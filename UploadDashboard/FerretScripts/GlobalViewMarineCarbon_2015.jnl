! see
! http://www.esrl.noaa.gov/gmd/ccgg/globalview/co2/co2_version.html  says,
!
!    The Reference Marine Boundary Layer matrix, which was provided in previous 
!    releases, is no longer available. Instead, NOAA has made a more comprehensive 
!    MBL reference product available here.  

! The link references this page :
! http://www.esrl.noaa.gov/gmd/ccgg/mbl/
!
! and from there, http://www.esrl.noaa.gov/gmd/ccgg/mbl/data.php, downloading 
!the "surface" reference type gives us sine(latitude) vs time of the Reference 
! Marine Boundary Layer. 
!
! This file is in this format: 
! # Format = (F12.6,82(1X,F12.4))
! TIME  DATA  UNCERTAINTY  DATA  UNCERTAINTY, ...
! e.g.
!  1979.000000     334.6322       0.0912     334.6365       0.0975
! Read all the data, skipping TIME.  Then keep every second value, skipping the uncertainty.

! The data set lies in the YT plane.  Y is on equal intervals of SIN(latitude)
! The T axis divides each year into 48 equal pieces ("weeks")

! The date field is in year fractions -- an ugly unit for Ferret, since years
! are not all of equal length. Here we first confirm that the years can be
! read correctly and note that the number of points on it is correct

let nskip = 78

file/col=1/format="(1x,F12.6)"/var=date/skip=`nskip` tmp_co2_GHGreference.1148982998_surface.txt
let ndate = `date[i=@ngd]`
list/prec=9/i=1:5 date
list/prec=9/i=`ndate-5`:`ndate` date

can dat/all

! now define a time axis based on the description
define axis/t=4-jan-1979:4-jan-2016/npoints=`ndate`/unit=days/t0=1-jan-1970 tcarbon

! Input y axis with 82 values
define axis/y=1:82:1 yaxis

! define the grid of the dataset and set it up
define grid/y=yaxis/t=tcarbon gcarbon
file/skip=`nskip`/format="(12X, 82(1X,F12.4))"/col=82/var=co2_in/grid=gcarbon tmp_co2_GHGreference.1148982998_surface.txt

! define the latitude axis as 41 equal intervals of SIN(Y)
let sinlat = y[y=-1:1:.05]
define axis/y/units=degrees lats = asin(sinlat) * 180 / 3.14159

! Every second value of the input is CO2
let co2_only = co2_in[j=1:82:2]
let/title="NOAA greenhouse gas MBL surface reference 2015"/units="umol/mol" co2 = co2_only[gy=lats@asn]
shade co2; pause

! The co2 values aer followed by their uncertainty
let co2_uncertain = co2_in[j=2:82:2]
let/title="Uncertainty, NOAA greenhouse gas MBL surface reference 2015"/units="umol/mol" uncertain = co2_uncertain[gy=lats@asn]
shade uncertain; pause

save/clobber/file="/home/data/socat/gvco2_2015.nc" co2, uncertain

! Look at dataset differences
pause
can dat/all
can var/all

use "/home/data/socat/gvco2_2008.nc"
use "/home/data/socat/gvco2_2012.nc"
use "/home/data/socat/gvco2_2013.nc"
use "/home/data/socat/gvco2_2014.nc"
use "/home/data/socat/gvco2_2015.nc"

let/title="gvco2 2008" co2_08 = co2[d=1]
let/title="gvc02 2012" co2_12 = co2[d=2,gt=co2[d=1]]
let/title="gvc02 2013" co2_13 = co2[d=3,gt=co2[d=1]]
let/title="gvc02 2014" co2_14 = co2[d=4,gt=co2[d=1]]
let/title="gvc02 2015" co2_15 = co2[d=5,gt=co2[d=1]]
set data 1

! Difference between what we had as gvco2 2008 and 2012
shade co2_08 - co2_12

pause
! Difference between what we had as gvco2 2008 and 2013
shade co2_08 - co2_13

pause
! Difference between what we had as gvco2 2008 and 2014
shade co2_08 - co2_14

pause
! Difference between what we had as gvco2 2008 and 2015
shade co2_08 - co2_15

pause
! Difference between what we had as gvco2 2014 and 2015
shade co2_14 - co2_15
