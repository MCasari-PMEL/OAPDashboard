! Setup for OA preview plots
! Open the dataset, define the output file name
! Make definitions used by all the scripts

can view

! Open the input dataset
use "$1"

! Define the plot file name
DEFINE SYMBOL datasetid_str = `dataset_id`
DEFINE SYMBOL nplt = $2
IF `($nplt) LT 10` then DEFINE SYMBOL nplt = 0($nplt)

DEFINE SYMBOL result_plot_image_filename = ($result_directory)/($datasetid_str)_($nplt).gif

! Redefine the GIF-image file name, which prevents name clashes when there
! are multiple sessions.
DEFINE SYMBOL gifplot_name = ($result_plot_image_filename)_($session_pid)
SET GIFFILE "($gifplot_name)"

! Further setup, done once for all the outputs
IF ($preview_setup_done"0|*>1") THEN exit/script

! reset the green pen to a darker green...
ppl color,3,0,80,0    

! reset the blue pen to a lighter blue ...
ppl color,4,20,30,100   

! land map palette
DEFINE SYMBOL land_palette = gray_light

! Reset color 6 to light gray - line underlay on map, marking woce-4 points
ppl color,6,70,70,70 ! gray

! set up the color palette for WOCE flags
DEFINE SYMBOL woce_palette = woce2

can mode logo

ppl txlsze,0.08
ppl axlsze,0.08,0.08

LET nobs = `latitude[i=@ngd]`

! Define time axis
! If we cannot define the time axis, exit
SET MODE ignore
DEFINE AXIS/T/T0="1-Jan-1970"/UNITS=seconds tax = time
IF ($fer_last_error"0|*>1") THEN exit/script

CANCEL MODE ignore

let tt = t[gt=tax]; 
let tt1 = `tt[l=@min]`
let ttn = `tt[l=@max]`
LET monthsecs = 30*24*60*60
!IF `ttn-tt1 GT monthsecs` THEN DEFINE SYMBOL taxis_setting = PPL txtype,MON,DMY

! Define numeric variable on the time axis with the WOCE flags
SET VAR/NAME=WOCE_autocheck_in WOCE_autocheck
LET WOCE_autocheck_x = STRFLOAT(WOCE_autocheck_in)
LET WOCE_autocheck = RESHAPE(WOCE_autocheck_x, tt)

define view/x=0:1/y=0:1 full0
define view/x=0:1/y=0:1 full1
define view/x=0:1/y=0:1 full2
define view/x=0:1/y=0:1 full3

cancel mode nodata_label

! polygon plot style 
DEFINE SYMBOL ppstyle = /SYMBOL=17/siz=0.06
! line plot style 
DEFINE SYMBOL linestyle = /LINE/SYMBOL=21/siz=0.02
! Style for marking WOCE 4 locations
DEFINE SYMBOL woce4style = /SYMBOL=18/thick=1/siz=0.08/color=6

DEFINE SYMBOL preview_setup_done = 1
